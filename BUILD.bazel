load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@bzlws//rules:bzlws_copy.bzl", "bzlws_copy")
load("@rules_blender//:index.bzl", "blender_render")
load("@rules_pkg//pkg:zip.bzl", "pkg_zip")

package(default_visibility = ["//visibility:public"])

_VARIATIONS = [
    "color",
    "all-color",
    "white",
    "black",
    "folder",
]

_SIZES = [
    44,
    128,
    150,
    256,
]

[blender_render(
    name = "ecsact-{}".format(variation),
    blend_file = "logo.blend",
    render_format = "PNG",
    view_layers = [variation],
) for variation in _VARIATIONS]

[blender_render(
    name = "ecsact-{}{}".format(variation, size),
    blend_file = "logo.blend",
    python_script_args = [
        "--res_x",
        str(size),
        "--res_y",
        str(size),
    ],
    python_scripts = ["override_resolution.py"],
    render_format = "PNG",
    view_layers = [variation],
) for variation in _VARIATIONS for size in _SIZES]

filegroup(
    name = "ecsact_logo",
    srcs =
        [":ecsact-{}".format(variation) for variation in _VARIATIONS] +
        [":ecsact-{}{}".format(variation, size) for variation in _VARIATIONS for size in _SIZES],
)

write_file(
    name = "public_build_file",
    out = "BUILD",
    content = [
        "package(default_visibility = [\"//visibility:public\"])",
        "exports_files(glob([\"*\"]))",
    ],
)

pkg_zip(
    name = "ecsact_logo_archive",
    out = "ecsact_logo.tar.gz",
    srcs = [
        "logo.svg",
        "MODULE.bazel",
        ":ecsact_logo",
        ":public_build_file",
    ],
)

bzlws_copy(
    name = "copy_dist_archive",
    srcs = [":ecsact_logo_archive"],
    out = "dist/{FILENAME}",
    force = True,
)
